package text
	import data;
	
	export utf8, Utf8Stream,
	       read, print,
	       writeString, readAll, readWord,
	       readUntil, readUntilWord;
	

	def codecs = packages["@text"];

	def utf8 = data.Codec(codecs.read8, codecs.write8);
	
	class Utf8Stream(data.Stream)
		def create(src)
			super.create(src, utf8);
		end;
	end;

	def read(s) = readUntil(s, "\n");
	def print(s, fmt, args*) = writeString(s, fmt.subs.apply(args));
	def writeString(s, txt) = txt.each(s.write);
	
	def readAll(s)
		def res = "";
		def more()
			def c = s.read();
			if !c then
				return res;
			end;
			res = res + c;
			return more();
		end;
		return more();
	end;

	def readWord(s)
		return readUntil(s, " \t\n.\",:;()[]{}/\\&!~-?|");
	end;

	def readUntil(s, cs)
		def c, res = "";
		def more()
			c = s.read();
			if !c then
				if res == "" then
					return false;
				end;
				return res;
			end;
			if cs.contains(c) then
				return res;
			end;
			res = res + c;
			return more();
		end;
		return more();
	end;

	def readUntilWord(s, word)
		def c, res = "", match = "";
		def more()
			c = s.read();
			if !c then
				res = res + match;
				if res == "" then
					return false;
				end;
				return res;
			end;
			match = match + c;
			if !word.startsWith(match) then
				res = res + match;
				match = "";
			end;
			if match.size == word.size then
				return res;
			end;
			return more();
		end;
		return more();			
	end;

end;
