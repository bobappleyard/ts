package text

	def text = packages["@text"];

	export Stream, Utf8Stream;
	
	class Stream()
		def readLine()
			return this.readUntil("\n");
		end;
		def readWord()
			return this.readUntil(" \t\n");
		end;
		def readUntil(cs)
			def c, res = "";
			def more()
				c = this.readChar();
				if !c then
					if res == "" then
						return false;
					end;
					return res;
				end;
				if cs.contains(c) then
					return res;
				end;
				res = res + c;
				return more();
			end;
			return more();
		end;
		def readUntilWord(word)
			def c, res = "", match = "";
			def more()
				c = this.readChar();
				if !c then
					res = res + match;
					if res == "" then
						return false;
					end;
					return res;
				end;
				match = match + c;
				if !word.startsWith(match) then
					res = res + match;
					match = "";
				end;
				if match.size == word.size then
					return res;
				end;
				return more();
			end;
			return more();			
		end;
		def writeString(s)
			s.split().each(this.writeChar);
		end;
	end;
	
	class Utf8Stream(Stream)
		def create(s)
			this.src = s;
		end;

		def readByte() = this.src.readByte();
		def writeByte(b) = this.src.writeByte(b);
		def seek(from, by) = this.src.seek(from, by);
		def close() = this.src.close();

		def readChar() = text.read8(this);
		def writeChar(c) = text.write8(this, c);
	private
		def src;
	end;

end;
